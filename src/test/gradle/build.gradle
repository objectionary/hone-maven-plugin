/*
 * SPDX-FileCopyrightText: Copyright (c) 2024-2025 Objectionary.com
 * SPDX-License-Identifier: MIT
 */

plugins {
    id 'java'
    id 'application'
}

group = 'org.eolang.hone.gradle'
version = '0.0.0'

application {
    mainClass = 'org.eolang.gradle.App'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

test {
    useJUnitPlatform()
}

task hone(type: Exec, dependsOn: compileJava) {
    String ver = project.hasProperty('hone.version') ? project.property('hone.version') : '1.0-SNAPSHOT'
    commandLine 'mvn',
        "-Dhone.version=${ver}",
        "-Dhone.target=${buildDir}",
        "-Dhone.classes=${buildDir.toPath().relativize(sourceSets.main.output.classesDirs.singleFile.toPath())}",
        '-Dhone.rules=33-to-42',
        "org.eolang:hone-maven-plugin:${ver}:build",
        "org.eolang:hone-maven-plugin:${ver}:optimize"
}
compileJava.finalizedBy hone
classes.dependsOn hone
