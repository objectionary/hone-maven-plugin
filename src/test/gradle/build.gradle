/*
 * SPDX-FileCopyrightText: Copyright (c) 2024-2025 Objectionary.com
 * SPDX-License-Identifier: MIT
 */

plugins {
    id 'java'
    id 'application'
}

group = 'org.eolang.hone.gradle'
version = '0.0.0'

application {
    mainClass = 'org.eolang.gradle.App'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

test {
    useJUnitPlatform()
}

task hone(type: Exec, dependsOn: compileJava) {
    String ver = project.hasProperty('hone.version') ? project.property('hone.version') : '1.0-SNAPSHOT'
    String classesPath = sourceSets.main.output.classesDirs.singleFile.path - "${buildDir}${File.separator}"
    commandLine 'mvn',
        "-Dhone.version=${ver}",
        "-Dhone.target=${buildDir}",
        "-Dhone.classes=${classesPath}",
        '-Dhone.rules=33-to-42',
        "org.eolang:hone-maven-plugin:${ver}:build",
        "org.eolang:hone-maven-plugin:${ver}:optimize"
}
compileJava.finalizedBy hone
classes.dependsOn hone
